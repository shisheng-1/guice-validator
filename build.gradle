plugins {
    id 'com.gradle.build-scan' version '3.0'
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.1.2'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.quality' version '4.0.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '5.6.4'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencies {
    compile 'com.google.inject:guice:4.2.3'
    compile 'jakarta.validation:jakarta.validation-api:3.0.0'

    testCompile 'junit:junit:4.13.1'
    testCompile 'org.hibernate:hibernate-validator:7.0.0.Final'
    testCompile 'org.glassfish:javax.el:3.0.1b-12'
    testCompile 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0'
}

group = 'ru.vyarus'
description = 'Guice method validation through jakarta.validation implementation (hibernate-validator)'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

// java 11 auto module name
jar {
    manifest {
        attributes 'Automatic-Module-Name': "$project.group" + '.guice.validator'
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['guice', 'validation', 'hibernate-validator']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true


task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}
